# SIM grammar specification

Keep in mind that comments are not included in the syntatical and semantical analysis as they are removed before the lexical analysis. Their start delimiter and end delimiter are: **_{_** and **_}_**, respectively.

Moving ahead, down below is the complete grammar for the SIM language:

```
PROGRAM -> programa IDENTIFIER ; BLOCK .

BLOCK -> VARIABLES_DECLARATION_STAGE SUB-ROUTINES_DECLARATION_STAGE COMMANDS | VARIABLES_DECLARATION_STAGE COMMANDS | SUB-ROUTINES_DECLARATION_STAGE COMMANDS | COMMANDS

VARIABLES_DECLARATION_STAGE -> var VARIABLES_DECLARATION ; | var VARIABLES_DECLARATION ; VARIABLES_DECLARATION_STAGE

VARIABLES_DECLARATION -> IDENTIFIER : TYPE | IDENTIFIER , VARIABLES_DECLARATION

TYPE -> inteiro | booleano

SUB-ROUTINES_DECLARATION_STAGE -> PROCEDURE_DECLARATION ; | FUNCTION_DECLARATION ; | PROCEDURE_DECLARATION ; SUB-ROUTINES_DECLARATION_STAGE | FUNCTION_DECLARATION ; SUB-ROUTINES_DECLARATION_STAGE

PROCEDURE_DECLARATION -> procedimento IDENTIFIER ; BLOCK

FUNCTION_DECLARATION -> funcao IDENTIFIER : TYPE ; BLOCK

COMMANDS -> inicio COMMANDS_AUX fim

COMMANDS_AUX -> COMMAND ; | COMMAND | COMMAND ; COMMANDS_AUX

COMMAND -> ATTRIBUTION_COMMAND | PROCEDURE_OR_FUNCTION_CALL | CONDITIONAL_COMMAND | WHILE_COMMAND | READ_COMMAND | WRITE_COMMAND | FUNCTION_RETURN

ATTRIBUTION_COMMAND -> IDENTIFIER := EXPRESSION | IDENTIFIER :+ EXPRESSION | IDENTIFIER :- EXPRESSION

PROCEDURE_OR_FUNCTION_CALL -> IDENTIFIER

CONDITIONAL_COMMAND -> se ( EXPRESSION ) entao COMMAND | se ( EXPRESSION ) entao COMMAND senao COMMAND

WHILE_COMMAND -> enquanto ( EXPRESSION ) faca COMMAND

READ_COMMAND -> leia ( IDENTIFIER )

WRITE_COMMAND -> escreva ( IDENTIFIER ) | escreva ( " IDENTIFIER " )

EXPRESSION -> SIMPLE_EXPRESSION | SIMPLE_EXPRESSION RELATIONAL_LOGICAL_OPERATOR SIMPLE_EXPRESSION

RELATIONAL_LOGICAL_OPERATOR -> <> | = | < | <= | > | >= | e | ou

SIMPLE_EXPRESSION -> FACTOR | + FACTOR | - FACTOR

FACTOR -> VARIABLE | NUMBER | ( EXPRESSION ) | verdadeiro | falso | nao FACTOR

VARIABLE -> IDENTIFIER

FUNCTION_RETURN -> retorne IDENTIFIER

IDENTIFIER -> LETTER IDENTIFIER_AUX

IDENTIFIER_AUX -> LETTER | DIGIT | _ | LETTER IDENTIFIER_AUX | DIGIT IDENTIFIER_AUX | _ IDENTIFIER_AUX

NUMBER -> DIGIT | DIGIT NUMBER

DIGIT -> 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

LETTER -> a | b | c | d | e | f | g | h | i | j | k | l | m | n | o | p | q | r | s | t | u | v | w | x | y | z | A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V | W | X | Y | Z
```